apply plugin: 'io.github.gradle-nexus.publish-plugin'
apply plugin: 'org.jetbrains.dokka'

buildscript {
    apply from: 'common.gradle'
    apply from: rootProject.file("versions.gradle")

    repositories {
        mavenCentral()
        google()
        maven { url "https://jitpack.io" }
        mavenLocal()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$versions.androidgradleplugin"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
        classpath "io.github.gradle-nexus:publish-plugin:$versions.nexusPublish"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$versions.dokka"
    }
}

allprojects {
    repositories {
        google()
    }

    apply from: rootProject.file("versions.gradle")

    afterEvaluate {
        // Common library module configurations
        if (plugins.hasPlugin("com.android.library") || plugins.hasPlugin("com.android.application")) {
            android.compileSdkVersion = versions.compileSdkVersion

            android {
                defaultConfig {
                    minSdkVersion versions.minSdkVersion
                    targetSdkVersion versions.targetSdkVersion
                    vectorDrawables.useSupportLibrary = true
                }

                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_17
                    targetCompatibility JavaVersion.VERSION_17
                }
                kotlinOptions {
                    jvmTarget = '17'
                }

                // Include sources in the jar
                publishing {
                    singleVariant("release") {
                        withSourcesJar()
                        withJavadocJar()
                    }
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

apply from: "${rootDir}/publish-root.gradle"